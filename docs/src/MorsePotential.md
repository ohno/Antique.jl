```@meta
CurrentModule = Antique
```

# Morse Potential

The Morse potential is a model for inter-nuclear anharmonic vibration in a diatomic molecule.

## Definitions

```@docs; canonical=false
Antique.MorsePotential
```

#### Potential
```@docs; canonical=false
Antique.V(::MorsePotential, ::Any)
```

#### Eigenvalues
```@docs; canonical=false
Antique.E(::MorsePotential)
```

#### Maximum Quantum Number
```@docs; canonical=false
Antique.nₘₐₓ(::MorsePotential)
```

#### Eigenfunctions
```@docs; canonical=false
Antique.ψ(::MorsePotential, ::Any)
```

#### Generalized Laguerre Polynomials
```@docs; canonical=false
Antique.L(::MorsePotential, ::Any)
```

## Usage & Examples

[Install Antique.jl](@ref Install) for the first use and run `using Antique` before each use. The energy `E()`, wavefunction `ψ()`, potential `V()` and `nₘₐₓ()` will be exported. In this system, the model is generated by `MorsePotential` and several parameters `rₑ`, `Dₑ`, `k`, `µ` and `ℏ` are set as optional arguments.

```@example MP
# Parameters for H₂⁺
# https://doi.org/10.1002/slct.202102509
# https://doi.org/10.5281/zenodo.5047817
# https://physics.nist.gov/cgi-bin/cuu/Value?mpsme
rₑ =  1.997193319969992120068298141276
Dₑ = - 0.5 - (-0.602634619106539878727562156289)
k = 2*((-1.1026342144949464615+1/2.00) - (-0.602634619106539878727562156289)) / (2.00 - rₑ)^2
µ = 1/(1/1836.15267343 + 1/1836.15267343)
ℏ = 1.0

using Antique
MP = MorsePotential(rₑ=rₑ, Dₑ=Dₑ, k=k, µ=µ, ℏ=ℏ)
; #hide
```

Parameters:

```@repl MP
MP.rₑ
MP.Dₑ
MP.k
MP.µ
MP.ℏ
```

Maximum quantum number:

```@repl MP
nₘₐₓ(MP)
```

Eigenvalues:

```@repl MP
E(MP, n=0)
E(MP, n=1)
```

Potential energy curve:

```@example MP
using CairoMakie

f = Figure()
ax = Axis(f[1,1], xlabel=L"$r$", ylabel=L"$V(r)$",  limits=(0.0,20.0,-0.11,0.1))
lines!(ax, 0.1:0.01:20, r -> V(MP, r))
f
```

Wave functions:

```@example MP
using CairoMakie

# setting
fig = Figure()
axis = Axis(fig[1,1], xlabel=L"$r$", ylabel=L"$\psi(r)$")

# plot
lines!(axis, 0..5, x -> ψ(MP, x, n=0), label=L"n=0")
lines!(axis, 0..5, x -> ψ(MP, x, n=1), label=L"n=1")
lines!(axis, 0..5, x -> ψ(MP, x, n=2), label=L"n=2")
lines!(axis, 0..5, x -> ψ(MP, x, n=3), label=L"n=3")
lines!(axis, 0..5, x -> ψ(MP, x, n=4), label=L"n=4")

# legend
axislegend(axis, position=:lb, framevisible=false)

fig
```

Potential energy curve, Energy levels, Comparison with harmonic oscillator:

```@example MP
using Antique

# https://physics.nist.gov/cgi-bin/cuu/Value?mmusme
m = 206.7682830
μ = 1 / (1/m + 1/m)
MP = MorsePotential(μ=μ)
# @show nₘₐₓ(MP)

using CairoMakie

# settings
f = Figure()
ax = Axis(f[1,1], xlabel=L"$r$", ylabel=L"$V(r),~E_n,~\psi_n(r) \times 5 + E_n$", aspect=1, limits=(0.5,10,-0.105,0.007))
# hidespines!(ax)
# hidedecorations!(ax)

@show nₘₐₓ(MP)

for n in 0:nₘₐₓ(MP)
  # energy
  EMP = E(MP, n=n)
  lines!(ax, 0.1:0.01:15, r -> EMP > V(MP, r) ? EMP : NaN, color=:black, linewidth=2)
  hlines!(ax, E(MP, n=n), color=:black, linewidth=1, linestyle=:dash)
  # wave function
  lines!(ax, 0..10, x -> E(MP,n=n) + 0.0065*ψ(MP,x,n=n), linewidth=2)
end

#potential
lines!(ax, 0..10, x -> V(MP, x), color=:black, linewidth=2)

f
save("assets/fig/MorsePotential.png", f) # hide
; # hide
```

![](assets/fig/MorsePotential.png)

where, the potential of harmonic oscillator is defined as $V(r) \simeq \frac{1}{2} k (r - r_\mathrm{e})^2 + V_0$.

## Testing

Unit testing and Integration testing were done using computer algebra system ([Symbolics.jl](https://symbolics.juliasymbolics.org/stable/)) and numerical integration ([QuadGK.jl](https://juliamath.github.io/QuadGK.jl/stable/)). The test script is [here](https://github.com/ohno/Antique.jl/blob/main/test/MorsePotential.jl).

```@eval
using Markdown
using Antique
Markdown.parse(Antique.load("../../test/result/MorsePotential.log"))
```